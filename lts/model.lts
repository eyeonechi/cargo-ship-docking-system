/*
 * Ivan Ken Weng Chee 736901
 */

const MAX_SHIPS = 1
const NUM_PILOTS = 1
const NUM_TUGS = 5
const DOCKING_TUGS = 3
const UNDOCKING_TUGS = 2

BERTH
  = ( activate_shield -> deactivate_shield -> BERTH
    | dock_ship -> BERTH
    | undock_ship -> BERTH
    ).

OPERATOR
  = ( activate_shield -> OPERATOR
    | deactivate_shield -> OPERATOR
    ).

PILOT
  = (    acquire_ship
      -> acquire_docking_tugs
      -> arrival_depart
      -> dock_ship
      -> release_docking_tugs
      -> unload_ship
      -> acquire_undocking_tugs
      -> undock_ship
      -> departure_arrive
      -> release_undocking_tugs
      -> release_ship
      -> PILOT
    ).

TUGS (N=NUM_TUGS) = TUGS_COUNT[N],
TUGS_COUNT[i:0..N]
  = ( when (i>=DOCKING_TUGS) acquire_docking_tugs -> TUGS_COUNT[i-DOCKING_TUGS]
    | when (i>=UNDOCKING_TUGS) acquire_undocking_tugs -> TUGS_COUNT[i-UNDOCKING_TUGS]
    | when ((i+DOCKING_TUGS)<=NUM_TUGS) release_docking_tugs -> TUGS_COUNT[i+DOCKING_TUGS]
    | when ((i+UNDOCKING_TUGS)<=NUM_TUGS) release_undocking_tugs -> TUGS_COUNT[i+UNDOCKING_TUGS]
    ).

ARRIVAL_ZONE (N=MAX_SHIPS) = ARRIVAL_COUNT[0],
ARRIVAL_COUNT[i:0..N]
  = ( when (i<MAX_SHIPS) arrive -> ARRIVAL_COUNT[i+1]
    | when (i>0) acquire_ship -> acquire_docking_tugs -> arrival_depart -> ARRIVAL_COUNT[i-1]
    ).

DEPARTURE_ZONE (N=MAX_SHIPS) = DEPARTURE_COUNT[0],
DEPARTURE_COUNT[i:0..N]
  = ( when (i<MAX_SHIPS) departure_arrive -> release_undocking_tugs -> release_ship -> DEPARTURE_COUNT[i+1]
    | when (i>0) depart -> DEPARTURE_COUNT[i-1]
    ).

PRODUCER
  = (arrive -> PRODUCER).

CONSUMER
  = (depart -> CONSUMER).

||PILOTS (N=NUM_PILOTS)
//= (p[i:1..N]:PILOT).
  = (forall[i:1..N] p[i]:(PILOT)).

||MAIN
  = (PRODUCER || ARRIVAL_ZONE(MAX_SHIPS) || PILOT || OPERATOR || BERTH || TUGS(NUM_TUGS) || DEPARTURE_ZONE(MAX_SHIPS) || CONSUMER).
